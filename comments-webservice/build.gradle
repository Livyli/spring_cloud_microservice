/*
* Build file for Comments-webservice
*/
project.ext {
    jarName = 'basic-comments-webservice'
    versionName = '0.0.1'
}

//apply common build
apply from: "$rootDir/module_build.gradle"

/**
 * These values(group & mainClassName) are required by the gradle docker plugin.
 *
 * The "group" value feeds into the docker tag and is required if you want to push the images
 * to docker hub.
 *
 * The "mainClassName" value tells which class has the "main" entry point for running the
 * Spring boot application.
 */
group = 'anilallewar'
mainClassName = 'com.anilallewar.microservices.comments.CommentsApplication'


dependencies {
    // Basic Spring boot with config client
    compile('org.springframework.cloud:spring-cloud-starter-config')
    compile("org.springframework.boot:spring-boot-starter-web")    
    compile("org.springframework.boot:spring-boot-starter-actuator")
    
    // Spring OAuth2 security
    compile("org.springframework.boot:spring-boot-starter-security")
    compile("org.springframework.security.oauth:spring-security-oauth2")
    compile("org.springframework.security:spring-security-jwt")
    
    // Eureka client
    compile('org.springframework.cloud:spring-cloud-starter-eureka')
    
    // Consumer driven contracts server
    testCompile('org.springframework.cloud:spring-cloud-starter-contract-verifier')
	
	// Zipkin tracing
	compile('org.springframework.cloud:spring-cloud-starter-zipkin')
}


buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        maven { url "http://repo.spring.io/snapshot" }
        maven { url "http://repo.spring.io/milestone" }
        maven { url "http://repo.spring.io/release" }
    }
    dependencies {
        // if using Stub Runner (consumer side) only remove this dependency
        classpath "org.springframework.cloud:spring-cloud-contract-gradle-plugin:1.1.1.RELEASE"
    }
}

apply plugin: 'spring-cloud-contract'
// Setup the package which contains base classes that the spring cloud contract test case would extend
contracts {
	packageWithBaseClasses = 'com.anilallewar.microservices.comments.contracts'
}

clean.doFirst {
	delete "${System.properties['user.home']}${File.separator}.m2${File.separator}repository${File.separator}anilallewar${File.separator}basic-comments-webservice"
	delete "${System.properties['user.home']}${File.separator}.m2${File.separator}repository${File.separator}anilallewar${File.separator}basic-comments-webservice-stubs"
}


apply plugin: 'maven-publish'
// Setup the artifact id with which the stubs jar would be published, the group id comes from the 'group' attribute defined earlier 
publishing {
    publications {
        mavenJava(MavenPublication) {
			artifactId jar.baseName
			version jar.version
            from components.java
        }
		stubs(MavenPublication) {
			artifactId "${jar.baseName}-stubs"
			version jar.version
			artifact verifierStubsJar
		}
    }
}